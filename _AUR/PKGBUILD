# Maintainer: shulhan <ms@kilabit.info>

pkgname=pakakeh.go-tools
pkgver=0.53.1.r42.g1ffdc03b
pkgrel=1
_sourcedir=$pkgname

pkgdesc="Miscellaneous CLI tools: ansua, bcrypt, epoch, ini, xtrk"
arch=(x86_64)
url='https://sr.ht/~shulhan/pakakeh.go'
license=('BSD')
conflicts=('share-tools')
replaces=('share-tools')

makedepends=(
	'go'
	'git'
)

provides=('pakakeh.go-tools')

source=(
	"$pkgname::git+https://git.sr.ht/~shulhan/pakakeh.go"
)

## For testing in local environment.
#_sourcedir=pakakeh.go-tools-local
#source=(
#	"$_sourcedir::git+file:///home/ms/go/src/git.sr.ht/~shulhan/pakakeh.go#branch=dev"
#)

md5sums=(
	'SKIP'
)

pkgver() {
	cd "${_sourcedir}"
	git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "${_sourcedir}"
	make build
}

package() {
	cd "${_sourcedir}"
	install -Dm755 _bin/ansua $pkgdir/usr/bin/ansua
	install -Dm755 _bin/bcrypt $pkgdir/usr/bin/bcrypt
	install -Dm755 _bin/epoch $pkgdir/usr/bin/epoch
	install -Dm755 _bin/go-test-coverhtml.sh  $pkgdir/usr/bin/go-test-coverhtml.sh
	install -Dm755 _bin/go-bench.sh           $pkgdir/usr/bin/go-bench.sh
	install -Dm755 _bin/go-mod-tip.sh         $pkgdir/usr/bin/go-mod-tip.sh
	install -Dm755 _bin/go-test-lint.sh       $pkgdir/usr/bin/go-test-lint.sh
	install -Dm755 _bin/httpdfs               $pkgdir/usr/bin/httpdfs
	install -Dm755 _bin/ini   $pkgdir/usr/bin/ini
	install -Dm755 _bin/xtrk  $pkgdir/usr/bin/xtrk
	install -Dm644 LICENSE    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
